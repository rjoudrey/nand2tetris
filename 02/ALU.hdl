// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

   // x0 = zx ? 0 : x
   Mux16(a=x, b=false, sel=zx, out=x0);

   // x1 = nx ? !x0 : x0
   Not16(in=x0, out=invX0);
   Mux16(a=x0, b=invX0, sel=nx, out=x1);
   
   // y0 = zy ? 0 : y
   Mux16(a=y, b=false, sel=zy, out=y0);
   
   // y1 = ny ? !y0 : y0
   Not16(in=y0, out=invY0);
   Mux16(a=y0, b=invY0, sel=ny, out=y1);   
   
   // z0 = f ? (x + y) : (x & y)
   Add16(a=x1, b=y1, out=x1PlusY1);
   And16(a=x1, b=y1, out=x1AndY1);
   Mux16(a=x1AndY1, b=x1PlusY1, sel=f, out=z0);
   
   // z1 = no ? !z0 : z0
   Not16(in=z0, out=invZ0);
   Mux16(a=z0, b=invZ0, sel=no, out=z1);
   
   // zr = z1 == 0
   Or16(a=z1, b=z1, out[0..7]=lower, out[8..15]=higher); // Extract lower/higher 8 bit groups
   Or8Way(in=lower, out=zr0);
   Or8Way(in=higher, out=zr1);
   Or(a=zr0, b=zr1, out=zr2);
   Not(in=zr2, out=zr);
   
   // ng = z1 < 0
   // Does highest bit equal 1? Then it's negative.   
   Or16(a=z1, b=z1, out[15]=highest); // Extract highest bit
   And(a=highest, b=true, out=ng);

   // out = z1
   Or16(a=z1, b=z1, out=out);
}